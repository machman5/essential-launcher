/*
 * Copyright (C) 2018  Clemens Bartz
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'

repositories {
    jcenter()
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "de.clemensbartz.android.launcher"
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 20
        versionName "2.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        animationsDisabled true

        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    lintOptions {
        lintConfig file("lint.xml")

        ignoreWarnings false
        checkAllWarnings true
        warningsAsErrors true
        abortOnError true

        htmlReport true
        xmlReport true
        noLines true
        showAll true
    }
}

jacoco {
    toolVersion = '0.8.5'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['test', 'createDebugCoverageReport', 'lintRelease']) {
    reports {
        xml.enabled = true
        html.enabled = false
        csv.enabled = false
    }
}

task checkstyleMain(type: Checkstyle) {
    source 'src/main/java'
    include '**/*.java'
    exclude '**/gen/**'
    showViolations true

    reports {
        xml {
            enabled true
        }
        html {
            enabled true
        }
    }

    classpath = files()
}

tasks.sonarqube.dependsOn jacocoTestReport, check
tasks.check.dependsOn checkstyleMain

checkstyle {
    ignoreFailures false
    maxErrors 0
    maxWarnings 0
}

sonarqube {
    properties {
        property "sonar.projectKey", "clemensbartz_essential-launcher"
        property "sonar.organization", "clemensbartz"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", System.getenv("SONAR_LOGIN")

        property "sonar.java.checkstyle.reportPaths", "build/reports/"
        property "sonar.android.lint.report", "build/outputs/lint-results-release.xml"
        property "sonar.junit.reportsPath", "build/test-results/testDebugUnitTest"
        property "sonar.java.coveragePlugin", "jacoco"

        property "sonar.sources", "src/main/java"
        property "sonar.tests", "src/test/java, src/androidTest/java"

        def unit = fileTree(dir: project.projectDir, includes: ['**/*.exec']).files;
        def ui = fileTree(dir: project.projectDir, includes: ['**/*.ec']).files;
        unit.addAll(ui);
        def files = unit.join(", ");
        property "sonar.jacoco.reportPaths", files
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly 'androidx.annotation:annotation:1.1.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    // The following dependency is set lower intentionally, see issue #76
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.core:core:1.1.0'
    androidTestImplementation 'androidx.viewpager:viewpager:1.0.0' // somehow, espresso needs thisâ€¦
}
